package eu.trentorise.game.model

import eu.trentorise.game.core.Utility;
global Utility utils;

rule "reward a group challenge"
lock-on-active
when
    $reward : Reward($calculationPointConceptRef :  calculationPointConcept) 
    $pcCalculation: PointConcept(name == $calculationPointConceptRef.name)
    $pcTarget : PointConcept(name == $reward.targetPointConcept.name)
    Player($player: id)

then
	
	//WARNING: here we are assuming that calculation point and target point belong 
	//to the same point concept, and hence, there is no need for conversion
	Double percentage = $reward.getPercentage();
	
	Double base = $pcCalculation.getPeriodCurrentScore($calculationPointConceptRef.getPeriod());
	utils.log(String.format("Reward calculated on %s - %s: %s", $calculationPointConceptRef.getName(), $calculationPointConceptRef.getPeriod(),base));

	double rewardScore = (double) Math.round(base * percentage / 100);
	if($reward.getThreshold() > 0 && rewardScore > $reward.getThreshold()) {
		rewardScore = $reward.getThreshold(); 
	}
	
	// not a percentage bonus, try to use the fixed one
	if(rewardScore <= 0) {
		Map<String,Double> playerBonus = $reward.getBonusScore();
		Double bonus = playerBonus.get($player);
		if(bonus != null) {
			rewardScore = bonus;
		}
	}
	
	$pcTarget.setScore($pcTarget.getScore() + rewardScore);
			
	update($pcTarget);

end

