package eu.trentorise.game.model

import eu.trentorise.game.notification.BadgeNotification;

import eu.trentorise.game.core.Utility;

global Utility utils;


/* ***************************************************************************
E-MOTION  
  
*************************************************************************** */

rule "e-motion badge"
	salience -1000
when
    InputData(data['bikesharing'] == true)
    $bc : BadgeCollectionConcept(name == "special", badgeEarned not contains 'e-motion')
    Game( $gameId: id)
    Player( $playerId : id)
then
	utils.log("apply \'e-motion badge\'");
	$bc.getBadgeEarned().add('e-motion');
	insert( new BadgeNotification($gameId,$playerId,'e-motion'));
	update( $bc );
end

/* ***************************************************************************
PARK  
  
*************************************************************************** */

rule "park badge"
	salience -1000
when
    InputData( $park : data['park'] != null)
    $bc : BadgeCollectionConcept(name == "special", badgeEarned not contains ($park+'-park'))
    Game( $gameId: id)
    Player( $playerId : id)
then
	utils.log("apply  \'"+$park+"-park badge\'");
	$bc.getBadgeEarned().add((String) $park+'-park');
	insert( new BadgeNotification($gameId,$playerId,$park+'-park'));
	update( $bc );
end


/* ***************************************************************************
IMPACT 0
  
*************************************************************************** */
rule "zero impact badge"
	salience -1000
when
    InputData((data['walkDistance'] != null || data['bikeDistance'] != null) && ((data['busDistance'] == null || data['busDistance'] == 0) &&  (data['carDistance'] == null || data['carDistance'] == 0)))
    $bc : BadgeCollectionConcept(name == "special", badgeEarned not contains 'zero-impact')
    Game( $gameId: id)
    Player( $playerId : id)
then
	utils.log("apply \'zero-impact badge\'");
	$bc.getBadgeEarned().add('zero-impact');
	insert( new BadgeNotification($gameId,$playerId,'zero-impact'));
	update( $bc );
end