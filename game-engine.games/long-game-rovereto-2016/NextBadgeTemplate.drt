template header

ch_ID
ch_player
ch_badge_collection
ch_point_type

package eu.trentorise.game.model

// vinci gli <X> prossimi Badge in Badge Collection <Y> e vinci un bonus di <Z> Green Points
/* Parameters for the template
ch_ID = unique Challenge ID (ALWAYS!): obtained via UUID
ch_badge_collection = badge collection to observe for updates
ch_player = unique Player ID 
ch_point_type
*/

/*
 CustomData to have   []=used by
    ID is a unique string that identifies the challenge instance
  
  - ch_ID_startChTs [rule] [presentation]
  - ch_ID_endChTs [rule] [presentation]
  - ch_ID_bonus [presentation]
  - ch_ID_type [presentation]
  - ch_ID_counter [rule] [presentation]
  - ch_ID_target [rule] [presentation]
  - ch_ID_badge_collection [rule] [presentation]
  - ch_ID_success [rule] [presentation]
  - ch_ID_point_type [presentation] type of bonus point -> it must contain the name of PointConcept ("green leaves" for example)
*/

template "WinNextBadge"

rule 'ch_@{ch_ID}_trace'
when
	$bu : BadgeUpdate(this.collection == "@{ch_badge_collection}", $b: this.badge) 
	Player( $playerId : id == '@{ch_player}')
	$c : CustomData($now : System.currentTimeMillis(), this['ch_@{ch_ID}_startChTs'] <= $now, this['ch_@{ch_ID}_endChTs'] > $now, $counter : this['ch_@{ch_ID}_counter'])
then
	log ("Badge Collection for Player " + $playerId + " updated with: " + $b.toString());
	if ($counter == null) {
		$c.put("ch_@{ch_ID}_counter", 1);
	} else {
		$c.put("ch_@{ch_ID}_counter", (Integer) $counter + 1);
	}
	retract ($bu);
	update($c);	
end

rule 'ch_@{ch_ID}_check'
when
	$c : CustomData($now : System.currentTimeMillis(), this['ch_@{ch_ID}_startChTs'] <= $now, this['ch_@{ch_ID}_endChTs'] > $now, this['ch_@{ch_ID}_counter'] == this['ch_@{ch_ID}_target'],
		this['ch_@{ch_ID}_success'] != true)
	Player(id == '@{ch_player}')
	$pc : PointConcept(name == "@{ch_point_type}")
then
	$pc.setScore($pc.getScore() + (Integer) $c.get("ch_@{ch_ID}_bonus"));
	$c.put("ch_@{ch_ID}_success",true);
	insert(new PointDelta((Integer)$c.get("ch_@{ch_ID}_bonus"),"@{ch_point_type}"));
	log('ch_@{ch_ID} success');
	update($c);
	update($pc); 
end

end template