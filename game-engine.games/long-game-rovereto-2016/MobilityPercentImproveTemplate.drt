template header

ch_ID
ch_mode
ch_player
ch_point_type

package eu.trentorise.game.model

// CH1variant - Aumenta di <X>% i km fatti in mode <M> e avrai <Y> punti bonus

/* Parameters for the template:
ch_ID = unique Challenge ID (ALWAYS!): obtained via UUID
ch_mode
ch_player = unique Player ID - for now simply Object.toString()
ch_point_type
*/

/*  parameters retrieved from custom data
CustomData to have   []=used by
    ID is a unique string that identifies the challenge instance
    
  - ch_ID_startChTs [rule] [presentation]
  - ch_ID_endChTs [rule] [presentation]
  - ch_ID_Km_traveled_during_challenge [rule] [presentation]
  - ch_ID_target [rule] [presentation]
  - ch_ID_bonus [presentation]
  - ch_ID_type [presentation]
  - ch_ID_point_type [presentation] type of bonus point -> it must contain the name of PointConcept ("green leaves" for example)
  - ch_ID_success [rule] [presentation]
  - ch_ID_mode [presentation] modality of trip [values allowed: walk, bike, bikesharing, train, bus, car]
*/

	
template MobilityPercentImproveTemplate

rule "ch_@{ch_ID}_trace"
lock-on-active
when 
	InputData($distance: data["@{ch_mode}"] != null)	
	$c: CustomData($now: System.currentTimeMillis(), this['ch_@{ch_ID}_startChTs'] <= $now, this['ch_@{ch_ID}_endChTs'] > $now, $counter : this['ch_@{ch_ID}_Km_traveled_during_challenge']	)
	Player(id == "@{ch_player}") 
then
	if($counter == null) {
		$c.put('ch_@{ch_ID}_Km_traveled_during_challenge',(Double) $distance);	
	}else {
		$c.put('ch_@{ch_ID}_Km_traveled_during_challenge', (Double)$counter + (Double) $distance);
	}
	log('ch_@{ch_ID} update');
	update($c); 
end

rule 'ch_@{ch_ID}_check'
when
	Player(id == "@{ch_player}")
	$c: CustomData($now: System.currentTimeMillis(), this['ch_@{ch_ID}_startChTs'] <= $now, this['ch_@{ch_ID}_endChTs'] > $now, this['ch_@{ch_ID}_Km_traveled_during_challenge'] >= this['ch_@{ch_ID}_target'], this['ch_@{ch_ID}_success'] != true)
	$pc : PointConcept(name == "@{ch_point_type}")
then
	$pc.setScore($pc.getScore() + (Integer)$c.get("ch_@{ch_ID}_bonus"));
	$c.put('ch_@{ch_ID}_success',true);
	insert(new PointDelta((Integer)$c.get("ch_@{ch_ID}_bonus"),"green leaves")); 
	log('ch_@{ch_ID} successful');
	update($c);
	update($pc); 
end
	
end template